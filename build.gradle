plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.7.18'
  id 'com.github.ben-manes.versions' version '0.41.0'
  id 'org.sonarqube' version '5.0.0.4638'
  id "org.flywaydb.flyway" version "8.5.13"
  id 'info.solidsoft.pitest' version '1.15.0'
  id 'uk.gov.hmcts.java' version '0.12.61'
}

group = 'uk.gov.hmcts.reform.rd'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

task fortifyScan(type: JavaExec)  {
  mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  // This is a temporary fix to prevent the nightly build from failing if the Fortify scan detects issues
  ignoreExitValue = true
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.53.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ignoreFailures = true
  ruleSets = [
    'category/java/errorprone.xml',
    'category/java/bestpractices.xml'
  ]
}

jacoco {
      toolVersion = "0.8.7"
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

pitest {
  junit5PluginVersion = '1.0.0'
  targetClasses = ['uk.gov.hmcts.reform.locationrefdata.*']
  testSourceSets = [sourceSets.test, sourceSets.functionalTest]
  excludedClasses = [
    'uk.gov.hmcts.reform.locationrefdata.configuration.*',
    'uk.gov.hmcts.reform.locationrefdata.LrdLoadApplication',
    'uk.gov.hmcts.reform.locationrefdata.constants.*',
    'uk.gov.hmcts.reform.locationrefdata.camel.util.LrdExecutor',
    'uk.gov.hmcts.reform.locationrefdata.camel.util.LogDto',
    'uk.gov.hmcts.reform.locationrefdata.camel.binder.*'
  ]
  threads = 6
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  useClasspathFile = true
  mutationThreshold = 75
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "rd-location-ref-data-load"
    property "sonar.projectKey", "uk.gov.hmcts.reform:rd-location-ref-data-load"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/locationrefdata/configuration/**," +
    "src/main/java/uk/gov/hmcts/reform/locationrefdata/LrdLoadApplication.java," +
    "src/main/java/uk/gov/hmcts/reform/locationrefdata/camel/binder/**"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: '1.77'
    dependencySet(group: 'com.google.guava', version: '32.1.3-jre') {
      entry 'guava'
    }

    //CVE-2022-40152
    dependencySet(group: 'com.fasterxml.woodstox', version: '6.5.0') {
            entry 'woodstox-core'
    }

    //CVE-2023-24998
    dependencySet(group: 'commons-fileupload', version: '1.5') {
      entry 'commons-fileupload'
    }

    //CVE-2021-22044
    dependencySet(group: 'org.springframework.cloud', version: '3.1.9') {
        entry 'spring-cloud-starter-openfeign'
        entry 'spring-cloud-openfeign-core'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://jitpack.io'
  }
}

def versions = [
  junit               : '5.9.2',
  junitPlatform       : '1.9.2',
  reformLogging       : '6.0.1',
  springBoot          : '2.7.18',
  springfoxSwagger    : '2.9.2',
  serenity            : '2.0.76',
  lombok              : '1.18.32',
  flyway              : '6.5.7',
  camel               : '3.8.0',
  gradlePitest        : '1.5.1',
  pitest              : '1.16.1',
  jackson             : '2.14.2',
  log4j               : '2.17.1',
  springVersion       : '5.3.36',
  logback             : '1.2.13',
  bouncycastle        : '1.77'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
  implementation (group: 'org.springframework.boot', name: 'spring-boot-starter-web'){
    exclude group: 'org.glassfish', module: 'jakarta.el'
  }
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger

  implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"
  implementation "com.github.hmcts.java-logging:logging-appinsights:${versions.reformLogging}"
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE'
  implementation ('com.github.hmcts:properties-volume-spring-boot-starter:0.1.0'){
    exclude group: 'org.glassfish', module: 'jakarta.el'
  }

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
  implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
  implementation group: 'org.flywaydb', name: 'flyway-core', version: versions.flyway
  implementation group: 'org.springframework.security', name: 'spring-security-core', version: '5.7.11'
  implementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: versions.bouncycastle

  implementation group: 'org.springframework', name: 'spring-aop', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-aspects', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-context', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-context-support', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-expression', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-jcl', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-jdbc', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-orm', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-tx', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-web', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-webmvc', version: versions.springVersion

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4j
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.13.2.20220324', ext: 'pom'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson

  implementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'

  implementation ('com.github.hmcts:data-ingestion-lib:0.5.2.5') {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
    exclude group: 'org.glassfish', module: 'jakarta.el'
  }
  //Fix for CVE-2021-29425
  implementation 'commons-io:commons-io:2.11.0'
  implementation group: 'org.apache.camel', name: 'camel-bom', version: versions.camel, ext: 'pom'
  implementation group: 'org.apache.camel.springboot', name: 'camel-spring-boot-dependencies', version: versions.camel

  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
  implementation group: 'org.springframework', name: 'spring-core', version: versions.springVersion
  implementation group: 'org.springframework', name: 'spring-beans', version: versions.springVersion

  implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-config-server', version: '3.1.9'

  // Fix for CVE-2022-45047, CVE-2023-35887
  implementation group: 'org.apache.sshd', name: 'sshd-common', version: '2.9.3'
  implementation group: 'org.apache.sshd', name: 'sshd-core', version: '2.9.3'
  implementation group: 'org.apache.sshd', name: 'sshd-osgi', version: '2.9.3'
  implementation group: 'org.apache.sshd', name: 'sshd-sftp', version: '2.9.3'

  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '12.1'

  compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.4.0'
  testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

  testImplementation (group: 'net.serenity-bdd', name: 'serenity-core', version: '3.1.15')
    {
      exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
      exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }
  testImplementation (group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity)
    {
      exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
      exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }
  testImplementation (group: 'net.serenity-bdd', name: 'serenity-spring', version: '3.1.15')
    {
      exclude group: 'com.vladsch.flexmark', module: 'flexmark-all'
      exclude group: 'javax.websocket', module: 'javax.websocket-api'
    }

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
  testImplementation 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.15.0'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.test.runtimeClasspath

  testImplementation group: 'org.apache.camel', name: 'camel-test-spring-junit5', version: versions.camel
  testImplementation group: 'org.apache.camel', name: 'camel-test-junit5', version: versions.camel
  testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.16.2'
  testImplementation group: 'org.springframework.batch', name: 'spring-batch-test', version: '4.3.10'
  testImplementation group: 'org.javatuples', name: 'javatuples', version: '1.2'

  testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.4.1'
  testImplementation 'com.github.hmcts:fortify-client:1.2.0:all'

}

dependencyManagement {

  dependencies {
    // CVE-2021-42340
    // CVE-2023-28709
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.89') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-el'
      entry 'tomcat-embed-websocket'
    }

    //        Resolves CVE-2023-4586
    dependencySet(group: 'io.netty', version: '4.1.110.Final') {
      entry 'netty-buffer'
      entry 'netty-codec'
      entry 'netty-codec-http'
      entry 'netty-codec-socks'
      entry 'netty-common'
      entry 'netty-handler'
      entry 'netty-handler-proxy'
      entry 'netty-resolver'
      entry 'netty-transport'
      entry 'netty-transport-native-unix-common'
    }
  }
}

mainClassName = 'uk.gov.hmcts.reform.locationrefdata.LrdLoadApplication'

bootJar {
  getArchiveFileName().set(provider {
    'rd-location-ref-data-load.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

// Fix for CVE-2021-21295 & need to be removed with new Azure blob version
configurations.all {
  resolutionStrategy.eachDependency { details ->
    // Fix for CVE-2020-21913 & needs to be removed when camel-azure-starter is upgraded to latest version in data-ingestion-library
    if (details.requested.group == 'com.ibm.icu') {
      details.useVersion "66.1"
    }
  }

  resolutionStrategy.eachDependency { details ->
    if (details.requested.group == 'org.bouncycastle'
      && (details.requested.name == 'bcpkix-jdk18on'
      || details.requested.name == 'bcprov-jdk18on')
    ){
      details.useVersion versions.bouncycastle
    }
  }

      exclude group: 'org.codehaus.jettison', module: 'jettison'
}

dependencies {
  modules {
    module("org.bouncycastle:bcpkix-jdk15on") {
      replacedBy("org.bouncycastle:bcpkix-jdk18on")
    }
    module("org.bouncycastle:bcprov-jdk15on") {
      replacedBy("org.bouncycastle:bcprov-jdk18on")
    }
  }
}

test {
  useJUnitPlatform()
}

functional {
  useJUnitPlatform()
}

rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

